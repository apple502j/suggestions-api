ext.support_minecraft_versions = project.hasProperty("support_minecraft_versions")
        ? support_minecraft_versions : minecraft_version

ext.getVersionFullName = { String platform ->
    return "${mod_version}+${support_minecraft_versions}-${platform}"
}

ext.getMinecraftDependsVersion = { String platform ->
    final var segments = support_minecraft_versions.split("-")

    if (platform == 'forge') {
        if (segments.length == 1)
            return "[${support_minecraft_versions}]"

        return "[${segments[0]},${segments[1]}]"
    }

    if (segments.length == 1)
        return "=${support_minecraft_versions}"

    return ">=${segments[0]} <=${segments[1]}"
}

processResources {
    from project(":general").sourceSets.main.resources

    inputs.property "version", getVersionFullName(platform)
    inputs.property "minecraft_version", getMinecraftDependsVersion(platform)
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": getVersionFullName(platform),
                "minecraft_version": getMinecraftDependsVersion(platform)
    }
}
