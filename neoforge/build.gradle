plugins {
    id 'net.neoforged.gradle' version '[6.0.18,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}


ext.platform = 'neoforge'

apply from: '../util.gradle'

archivesBaseName = "${project.archives_base_name}-${ext.getVersionFullName(platform)}"

minecraft {
    mappings channel: 'official', version: minecraft_version

    runs {
        configureEach {
            workingDirectory rootProject.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                emogg {
                    source sourceSets.main
                    source project(":general").sourceSets.main
                }
            }
        }

        client {}
        server {}
    }
}

dependencies {
    minecraft "net.neoforged:forge:${minecraft_version}-${neoforge_version}"

    compileOnly project(":general")

    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
}

mixin {
    add sourceSets.main, "suggestionsapi.refmap.json"
    config "suggestionsapi.mixins.json"
}

tasks.withType(JavaCompile).configureEach {
    source(project(":general").sourceSets.main.allSource)
}

jar.finalizedBy('reobfJar')

def refmap_file = "suggestionsapi.refmap.json"

processResources {
    from project(":general").sourceSets.main.resources

    inputs.property "refmap_file", refmap_file
    filteringCharset "UTF-8"

    filesMatching("suggestionsapi.mixins.json") {
        expand "refmap_file": refmap_file
    }
}