plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT'
    id 'maven-publish'
}

apply from: '../util.gradle'

ext.platform = 'fabric'
archivesBaseName = "${project.archives_base_name}-${getVersionFullName(platform)}"

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()

    compileOnly project(":general")

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}+${project.minecraft_version}"
}

tasks.withType(JavaCompile).configureEach {
    source(project(":general").sourceSets.main.allSource)
}

processResources {
    from project(":general").sourceSets.main.resources

    inputs.property "version", getVersionFullName(platform)
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": getVersionFullName(platform)
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

loom {
    runs {
        client {
            client()
            ideConfigGenerated(true)
            runDir("../run")
        }
        server {
            server()
            ideConfigGenerated(true)
            runDir("../run")
        }
    }
}